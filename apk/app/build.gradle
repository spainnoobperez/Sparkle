apply plugin: 'com.android.application'

class MyVersion
{
    def revision
    MyVersion()
    {
        def out = new StringBuilder()
        def err = new StringBuilder()
        def p = "git rev-list --count HEAD".execute()
        p.consumeProcessOutput(out, err)
        p.waitForProcessOutput(out, err)
        p.waitFor()
        revision = out.toInteger()
    }
    String toString()
    {
        return "0.${revision}"
    }
    int toInteger()
    {
        return revision
    }
}

android {

    MyVersion v = new MyVersion()

    compileSdkVersion 28
    defaultConfig {
        applicationId 'com.sion.sparkle'
        minSdkVersion 23
        targetSdkVersion 28
        versionCode = v.toInteger()
        versionName = v.toString()
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.debug

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
        debug {
            ndk {
                abiFilters 'armeabi-v7a'
            }
            externalNativeBuild {
                cmake {
                    cppFlags += "-DX_DEBUG"
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
}


allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
